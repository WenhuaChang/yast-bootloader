/**
 * File:
 *      include/bootloader/routines/switcher.ycp
 *
 * Module:
 *      Bootloader installation and configuration
 *
 * Summary:
 *      Functions for choosing proper bootloader-specific functions
 *
 * Authors:
 *      Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 *
 */

{
    import "BootGRUB";
    import "BootLILO";
    import "BootSILO";
    import "BootMILO";
    import "BootABOOT";
    import "BootELILO";
    import "BootS390";
    import "BootPPC";
    import "BootCommon";

    /**
      * Get map of main functions for bootloader
      * @param bootloader string bootloader name
      * @return map of function
      */
    global define map getFunctions (string bootloader) ``{
	map bl_functions = $[
	    "lilo"  : ``(BootLILO::GetFunctions ()),
	    "grub"  : ``(BootGRUB::GetFunctions ()),
	    "silo"  : ``(BootSILO::GetFunctions ()),
	    "milo"  : ``(BootMILO::GetFunctions ()),
	    "aboot" : ``(BootABOOT::GetFunctions ()),
	    "elilo" : ``(BootELILO::GetFunctions ()),
	    "s390"  : ``(BootS390::GetFunctions ()),
	    "ppc"   : ``(BootPPC::GetFunctions ())
	];
	term gf = bl_functions[bootloader]:nil;
	return (map)eval(gf);
    }

    /**
      * Export bootloader-specific settings
      * @return map of settings
      */
    global define map blExport () ``{
        map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
        any toEval = functions["export"]:``(BootCommon::Export ());
        return (map)eval (toEval);
    }

    /**
      * Import settings to bootloader
      * @param settings map of settingss
      */
    global define void blImport (map settings) ``{
	map functions = getFunctions (BootCommon::getLoaderType (false));
	term toEval = functions["import"]:``(BootCommon::Import ());
	toEval = add (toEval, settings);
	eval (toEval);
	return;
    }

    /**
      * Read bootloader-specific settings
      * @return boolean true on success
      */
    global define boolean blRead () ``{
	map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
	any toEval = functions["read"]:``(BootCommon::Read ());
	return (boolean)eval (toEval);
    }

    /**
      * Reset bootloader-specific settings
      */
    global define void blReset () ``{
        map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
        any toEval = functions["reset"]:``(BootCommon::Reset ());
        eval (toEval);
    }

    /**
      * Propose bootloader settings
      */
    global define void blPropose () ``{
        map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
        any toEval = functions["propose"]:``(BootCommon::Propose ());
        eval (toEval);
    }

    /**
      * Save bootloader cfg. files
      * @return boolean true on success
      */
    global define boolean blSave () ``{
        map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
        any toEval = functions["save"]:``(BootCommon::Save ());
        return (boolean)eval (toEval);
    }

    /**
      * Get cfg. summary
      * @return list summary items
      */
    global define list<string> blSummary () ``{
        map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
        any toEval = functions["summary"]:``(BootCommon::Summary ());
        return (list<string>)eval (toEval);
    }

    /**
      * Update bootloader-specific settings
      */
    global define void blUpdate () ``{
        map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
        any toEval = functions["update"]:``(BootCommon::Update ());
        eval (toEval);
    }

    /**
      * Do the bootloader installation
      * @return boolean true on success
      */
    global define boolean blWrite () ``{
        map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
        any toEval = functions["write"]:``(BootCommon::Write ());
        return (boolean)eval (toEval);
    }

    /**
      * Update bootloader module cfg. sequence
      * @param ws_data map containing original sequence
      * @return map updates sequence
      */
    global define map blGetSequence (map ws_data) ``{
	map functions = getFunctions (BootCommon::getLoaderType (false));
	term toEval = functions["sequence"]:``(BootCommon::FixSequence ());
	toEval = add (toEval, ws_data);
	return (map)eval (toEval);
	return ws_data;
    }

    /**
      * Create map future cfg. files from internal variables
      * @return map filename -> contents
      */
    global define map blGetFiles () ``{
	map functions = getFunctions (BootCommon::getLoaderType (false));
// FIXME don't use any
	any toEval = functions["getfiles"]:nil;
	return (map)eval (toEval);
    }

    /**
      * Store map of future cfg. files to  internal variables
      * @param files map filename -> contents
      */
    global define void blSetFiles (map files) ``{
	map functions = getFunctions (BootCommon::getLoaderType (false));
	term toEval = functions["setfiles"]:nil;
	if (toEval == nil)
	    return nil;
	toEval = add (toEval, files);
	eval (toEval);
	return;
    }

}
