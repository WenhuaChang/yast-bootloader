/**
 * File:
 *      include/bootloader/routines/wh.ycp
 *
 * Module:
 *      Bootloader installation and configuration
 *
 * Summary:
 *      Functions for handling several widget and dialog functions
 *
 * Authors:
 *      Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 *
 */


{

    textdomain "bootloader";
    import "Bootloader";
    import "BootCommon";

    /**
      * Read widgets with listed names
      * @param names list of strings/symbols names of widgets
      * @return list of maps representing widgets
      */
    global define list getWidgets (list names) ``{
	map spec_widgets = Bootloader::blGetWidgets ();
	map common_widgets = BootCommon::GetWidgets ();
	return maplist (`w, names, ``(
	    eval (spec_widgets[w]:common_widgets[w]:$[])
	));
    }

    /**
      * Read popup widget with specified name
      * @param name any name of widgets
      * @return map representing widget
      */
    global define map getPopupWidget (any name) ``{
	map spec_widgets = Bootloader::blGetWidgets ();
	map common_widgets = BootCommon::GetWidgets ();
	spec_widgets = spec_widgets["tableentries"]:$[];
	common_widgets = common_widgets["tableentries"]:$[];
	return eval (spec_widgets[name]:common_widgets[name]:$[]);
    }

    /**
      * Read all popup widgets
      * @return map from widget id to map representing widget
      */
    global define map getPopupWidgets () ``{
	map spec_widgets = Bootloader::blGetWidgets ();
	map common_widgets = BootCommon::GetWidgets ();
	spec_widgets = eval (spec_widgets["tableentries"]:$[]);
	common_widgets = eval (common_widgets["tableentries"]:$[]);
	return union (common_widgets, spec_widgets);
    }

    /**
      * Set widgets according to internally stored settings
      * @param widgets list of maps represenging widgets
      */
    global define void readWidgets (list widgets) ``{
	foreach (`w, widgets, ``{
	    eval (w["restore"]:nil);
	});
    }

    /**
      * Handle change of widget after event generated
      * @param widgets list of maps represenging widgets
      * @param action any wizard sequencer symbol
      * @return any modified action (sometimes may be needed)
      */
    global define any handleChange (list widgets, any action) ``{
	if (! validateWidgetsIfNeeded (widgets, action))
	    return;
	list tosave = [`next, `ok, `apply, `accept];
	foreach (`w, widgets, ``{
	    tosave = merge (tosave, w["exits"]:[]);
	});
	tosave = toset (tosave);
	any ret = nil;
	foreach (`w, widgets, ``{
	    term t = w["handle"]:nil;
	    if (t != nil)
	    {
		any tmpret = eval (add (add (t, action), tosave));
		if (tmpret != action && tmpret != nil)
		    ret = tmpret;
	    }
	});
	if (ret != nil)
	    return ret;
	else
	    return action;
    }

    /**
      * Validate dialog contents for allow it to be saved
      * Checks whether next or other widget-specific event to save
      *  and leave dialog was generated
      * @param widgets list of widgets to validate
      * @param action any return value of UserInput
      * @return boolean true if everything is OK or no validation
      *  needed, false  if something is wrong
      */
    global define boolean validateWidgetsIfNeeded (list widgets, any action) ``{
	list needed = [`next, `ok, `apply, `accept];
	foreach (`w, widgets, ``{
	    list spec = w["exits"]:[];
	    needed = merge (needed, spec);
	});
	if (! contains (needed, action))
	    return true;
	boolean result = true;
	foreach (`w, widgets, ``{
	    term t = w["validate"]:nil;
	    if (t != nil)
		result = result && eval (t);
	});
	return result;
    }

    /**
      * Get symbol on which dialog should be exited
      * @param widgets list of widgets in dialog
      * @return list of symbols, which should be handled as exits
      */
    global define list getExitEvents (list widgets) ``{
	list exits = [`back, `next, `abort, `ok, `apply, `accept];
	foreach (`w, widgets, ``{
	    exits = merge (exits, w["exits"]:[]);
	});
	exits = toset (exits);
	return exits;
    }

    /**
      * Merge helps of specified widgets to one string
      * @param widgets list of widgets
      * @return string merged helps
      */
    global define string mergeHelps (list widgets) ``{
	list helps = maplist (`w, widgets, ``(w["help"]:nil));
	helps = filter (`h, helps, ``(h != nil));
	return mergestring (helps, "\n");
    }

}
