/**
 * File:
 *      include/bootloader/routines/ui.ycp
 *
 * Module:
 *      Bootloader installation and configuration
 *
 * Summary:
 *      User interface for bootloader installation/configuration
 *
 * Authors:
 *      Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 *
 */


{
    textdomain "bootloader";

    include "wizard/sequencer.ycp";
    include "ui/common_messages.ycp";

    include "bootloader/routines/wh.ycp";
    include "bootloader/routines/popups.ycp";
    include "bootloader/routines/convert.ycp";

    import "BootCommon";
    import "Bootloader";

    /**
      * Displaye popup for option to edit choosing
      * @param ot any option type (`global, "image", "other",...
      *   may be bootloader dependent)
      * @return any option identifies, nil if canceled
      */
    global define any getOptionChoicePopup (any ot) ``{
	list optnames = lookup (Bootloader::blOptions (),
	    BootCommon::getSectionType (BootCommon::current_section), []);
	optnames = sort (optnames);
	term widget = `HBox (`HSpacing (1), `VBox (
	    `VSpacing (1),
	    `ComboBox (`id (`optname), `opt (`editable),
		// combobox header
		_("&Selected Option"), optnames),
	    `VSpacing (1),
	    `HBox (
		`HStretch (),
		`PushButton (`id (`ok), `opt (`key_F10, `default),
		    OKButtonLabel ()),
		`HSpacing (1),
		`PushButton (`id (`cancel), `opt (`key_F9), CancelButtonLabel ()),
		`HStretch ()
	    ),
	    `VSpacing (1)
	), `HSpacing (1));
	UI::OpenDialog (widget);
	UI::SetFocus (`id (`optname));
	any ret = nil;
	any option = nil;
	while (ret != `ok && ret != `cancel)
	{
	    ret = UI::UserInput ();
	    if (ret == `ok)
	    {
		option = UI::QueryWidget (`id (`optname), `Value);
	    }
	}
	UI::CloseDialog ();
	if (ret == `cancel)
	    return nil;
	selected_opt = option;
	return option;
    }

    /**
      * Gets the widget for editing specified bootloader option
      * @param optname any option name
      * @param ot any option type (`global, "image", "other",...
      *   may be bootloader dependent)
      * @return term widget
      */
    global define term getSpecificOptionWidget (any optname, any ot) ``{
	map widget_data = getPopupWidget (optname);
        term widget = widget_data["widget"]:nil;
	map sect = BootCommon::current_section;
        if (widget == nil)
        {
            string type = widget_data["type"]:lookup (
                BootCommon::opttypes (), optname, "s");
            if (type == "b")
		// checkbox
                widget = `CheckBox (`id (`value),
		    widget_data["widget_label"]:_("&Set"),
                    sect[optname]:true);
            else if (type == "i")
		// intfield header
                widget = `IntField (`id (`value),
		    widget_data["widget_label"]:_("&Value"), 0, 999999,
                    sect[optname]:0);
            else
		// textentry header
                widget = `TextEntry (`id (`value),
		    widget_data["widget_label"]:_("&Value"),
                    sect[optname]:"");
        }
        return widget;
    }

    /**
      * Display and handle the popup for option
      * @param opttype any option type (`global, "image", "other",...
      *   may be bootloader dependent)
      * @param optname any option name
      * @return symbol `ok or `cancel
      */
    global define symbol singleOptionEditPopup (any opttype, any optname)``{
        map sect = BootCommon::current_section;
        if (optname == "")
        {
	    optname = getOptionChoicePopup (any opttype);
	    if (optname == nil)
		return;
        }

	BootCommon::current_option_name = optname;
	map widget_data = getPopupWidget (optname);

	y2milestone ("Popup widget: %1", widget_data);
        string label = sformat ("%1", widget_data["label"]:optname);
        term header = `HBox (
	    // heading / label
	    `Heading (_("Current Option: ")),
	    `Label (label),
	    `HStretch ()
	);
        term widget = getSpecificOptionWidget (optname, opttype);
	string help = widget_data["help"]:"";
	if (help == nil)
	{
	    help = "";
	}
        term contents = `HBox (`HSpacing (1), `VBox (
	    `VSpacing (1),
            `Left (header),
	    `VSpacing (1),
	    help == "" ? `VSpacing (0)
		: `Left (`Label (help)),
            `VSpacing (help == "" ? 0 : 1),
            `Left (`ReplacePoint(`id (`value_rp), widget)),
	    `VSpacing (1),
            `HBox (
                `HStretch(),
		`PushButton (`id (`ok), `opt (`key_F10, `default),
		    OKButtonLabel ()),
		`HSpacing (1),
                `PushButton (`id (`cancel), `opt (`key_F9), CancelButtonLabel ()),
                `HStretch ()
            ),
	    `VSpacing (1)
        ), `HSpacing (1));
        UI::OpenDialog (contents);
	if (widget_data["restore"]:nil != nil)
	    eval (widget_data["restore"]:nil);
	else
	    UI::SetFocus (`id (`value));
        any ret = nil;
        while (ret != `ok && ret != `cancel)
        {
            ret = UI::UserInput ();
	    if (ret != `ok)
	    {
		if (widget_data["handle"]:nil != nil)
		{
		    term toEval = widget_data["handle"]:nil;
		    toEval = add (add (toEval, ret), [`ok]);
		    eval (toEval);
		}
		else
		    UI::SetFocus (`id (`value));
	    }
	    if (ret == `ok)
	    {
		if (widget_data["validate"]:nil != nil
		    && ! eval (widget_data["validate"]:nil))
		{
		    ret = nil;
		}
	    }
	    if (ret == `ok)
	    {
		if (widget_data["handle"]:nil != nil)
		{
		    term toEval = widget_data["handle"]:nil;
		    toEval = add (add (toEval, `ok), [`ok]);
		    eval (toEval);
		}
		else
		{
		    BootCommon::current_section[optname]
			= UI::QueryWidget (`id (`value), `Value);
		}
		BootCommon::markOptionChanged (
		    BootCommon::current_section_name, optname);
	    }
        }
        UI::CloseDialog ();
	return ret;
    }

    global any selected_opt = nil;

    /**
      * Read seettings to the table of section configuration
      */
    global define void singleSectionRead () ``{
        map orig = BootCommon::current_section;
	list items = [];

	list options = Bootloader::blUpdateOptionsList (orig);
	options = filter (`o, options,
	    ``(o != "__order__" && o != "__intern__"));

	map opts = getPopupWidgets ();
	foreach (`o, options, ``{
	    map widget = opts[o]:$[];
	    string label = sformat ("%1", widget["label"]:o);
	    term toEval = widget["summary"]:nil;
	    string value = "";
	    if (toEval == nil)
		value = sformat ("%1",
		    BootCommon::current_section[o]:"");
	    else
		value = eval (toEval);
	    string chng = BootCommon::changed_settings[
		BootCommon::current_section_name, o]:false
		? "*" : "";
	    items = add (items, `item (`id (o), chng, label, value));
	});
	selected_opt = options[0]:nil;
	UI::ChangeWidget (`id (`settings), `Items, items);
	singleSectionHandle (`settings, []);
	UI::ChangeWidget (`id (`add), `Enabled, Bootloader::blOptions() != nil);
    }

    /**
      * Handle function of widget
      * @param op any operation to handle
      * @param tosave list of operations, which lead to closing & saving widget
      * @return any changed op (if needed)
      */
    global define any singleSectionHandle (any op, list tosave) ``{
	symbol ret = nil;
	any item = UI::QueryWidget (`id (`settings), `CurrentItem);
        if (op == `settings)
        {
            any prev = selected_opt;
            selected_opt = UI::QueryWidget (`id (`settings), `CurrentItem);
	    if (selected_opt == "__sep1" || selected_opt == "__sep2"
		|| selected_opt == "__sep3")
	    {
		map orig = BootCommon::current_section;
		list entries = Bootloader::blUpdateOptionsList (orig);
		entries = filter (`o, entries,
		    ``(o != "__order__" && o != "__intern__"));
                selected_opt = BootCommon::dividerHack (entries, prev,
		    selected_opt);
		UI::ChangeWidget(`id(`settings), `CurrentItem, selected_opt);
	    }
	    map cur = getPopupWidget (selected_opt);
	    UI::ChangeWidget (`id (`delete), `Enabled, cur["deletable"]:true);
        }
        else if (op == `edit)
        {
	    if (item == "__default_section")
		item = "__sections";
	    if (item == "__sections")
	    {
		BootCommon::sections_changed = false;
		return `sections;
	    }
           if (selected_opt == "__sep1" || selected_opt == "__sep2"
		    || selected_opt == "__sep3")
		return op;
            map options = Bootloader::blOptions ();
            ret = singleOptionEditPopup (
		BootCommon::getSectionType (BootCommon::current_section), item);
        }
        else if (op == `add)
        {
            ret = singleOptionEditPopup (
		BootCommon::getSectionType (BootCommon::current_section), "");
	    if (ret == `ok
		&& ! contains (
		    BootCommon::current_section["__order__"]:[], selected_opt)
		)
	    {
		BootCommon::current_section["__order__"] = add (
		    BootCommon::current_section["__order__"]:[], selected_opt);
	    }
        }
        else if (op == `delete && confirmOptionDeletePopup ())
        {
	    map cur = getPopupWidget (item);
	    term toEval = cur["delete"]:nil;
	    if (toEval != nil)
		eval (toEval);
	    BootCommon::current_section = remove (
		BootCommon::current_section, item);
	    BootCommon::one_section_changed = true;
	    ret = `ok;
        }
	// ORDERING BUTTONS
	else if (op == `up)
	{
	    list order
		= BootCommon::current_section["__order__"]:[];
	    order = BootCommon::filterOrder (order, getPopupWidgets ());
	    integer index = -1;
	    find (`o, order, ``{
		index = index + 1;
		return o == item;
	    });
	    order = BootCommon::swapItems (order, index, index - 1);
	    BootCommon::current_section["__order__"] = order;
		// FIXME global sect if will be needed
	    BootCommon::one_section_changed = true;
	    ret = `ok;
	}
	else if (op == `down)
	{
            list order
                = BootCommon::current_section["__order__"]:[];
	    order = BootCommon::filterOrder (order, getPopupWidgets ());
            integer index = -1;
            find (`o, order, ``{
                index = index + 1;
                return o == item;
            });
            order = BootCommon::swapItems (order, index, index + 1);
	    BootCommon::current_section["__order__"] = order;
		// FIXME global sect if will be needed
	    BootCommon::one_section_changed = true;
	    ret = `ok;
	}

	if ((op == `add || op == `edit || op == `delete || op == `up
	    || op == `down) && ret == `ok)
	{
	    if (BootCommon::current_section_name == `global)
	    {
		BootCommon::current_globals = BootCommon::current_section;
		BootCommon::storeSettings ();
		readWidgets (getWidgets(["globalsection"]));
	    }
	    else
	    {
		readWidgets (getWidgets(["singlesection"]));
	    }
	    UI::ChangeWidget (`id (`settings), `CurrentItem, item);
	}
	if (contains (tosave, op)
	    && BootCommon::current_section_name != `global)
	{
	    string ls = BootCommon::getSectionTitle ();
	    string old_name = BootCommon::current_section_name;
	    y2milestone ("Old name: %1", old_name);
	    map as = BootCommon::current_section;
	    as["__intern__"] = add (as["__intern__"]:$[], "changed", true);
	    boolean found = false;
	    BootCommon::current_sections = maplist (`s,
		BootCommon::current_sections,
	    ``{
		if (s[ls]:"" == old_name)
		{
		    found = true;
		    return as;
		}
		else
		    return s;
	    });
	    if (! found)
	    {
		BootCommon::current_sections = add (
		    BootCommon::current_sections,
		    BootCommon::current_section
		);
	    }
	    BootCommon::changed_settings[as[ls]:""]
		= BootCommon::changed_settings[old_name]:$[];
	    y2milestone ("Saving section");
	    if (BootCommon::one_section_changed)
		BootCommon::sections_changed = true;
	}
	if (`back == op)
	{
	    string current_l = BootCommon::current_section_name;
	    BootCommon::changed_settings[current_l]
		= eval (BootCommon::changed_settings_single_sect);
	}
	UI::SetFocus (`id (`settings));
    }

    /**
      * Handle function of widget
      * @param op any operation to handle
      * @param tosave list of operations, which lead to closing & saving widget
      * @return any changed op (if needed)
      */
    global define any globalSectionHandle (any op, list tosave) ``{
	any ret = singleSectionHandle (op, tosave);
	if (ret == `sections) // was op, maybe wrong fix
	{
            BootCommon::changed_settings_all_sections
		= eval (BootCommon::changed_settings);
	}
	if (op == `delete)
	    BootCommon::changed = true;
	return ret;
    }


    /**
      * Handle function of widget
      * @param op any operation to handle
      * @param tosave list of operations, which lead to closing & saving widget
      * @return any changed op (if needed)
      */
    global define any resetButtonHandle (any op, list tosave) ``{
        if (op == `restore_mbr)
        {
            boolean doit = restoreMBRPopup (BootCommon::mbrDisk);
	    y2milestone ("Rewrite MBR with saved one: %1", doit);
	    if (doit)
	    {
		boolean ret = BootCommon::restoreMBR (BootCommon::mbrDisk);
		if (ret)
		    // message popup
		    UI::MessagePopup (_("MBR restored successfully"));
		else
		    // message popup
		    UI::MessagePopup (_("Failed to restore MBR"));
	    }
            return;
        }

	if (! contains ([`scratch, `reread, `propose_deep, `propose], op))
	{
	    return;
	}
	Bootloader::Reset ();
	if (op == `scratch)
	{
	}
	else if (op == `reread)
	{
	    Bootloader::Read ();
	}
	else if (op == `propose_deep)
	{
	    import "BootGRUB";
	    BootGRUB::deep_propose = true;
	    Bootloader::Propose ();
	}
	else if (op == `propose)
	{
	    if (Bootloader::getLoaderType () == "grub")
	    {
		import "BootGRUB";
		BootGRUB::deep_propose = false;
	    }
	    Bootloader::Propose ();
	}
        BootCommon::fetchSettings ();
        BootCommon::current_section = eval (BootCommon::current_globals);
        BootCommon::current_section_name = `global;

	readWidgets (getWidgets(["globalsection"]));
    }

    /**
      * Ask for bootlaoder switch method
      * @param from string current spooler
      * @param to string new spooler
      * @param type of switching,
      *   `proposal for switching proposed because of disks changed
      *   `user for switching upon user's request
      * @return symbol method
      */
    global define symbol askSwitch (string from, string to, symbol type) ``{
	// warning - popup
	string label = type == `user ? _("Warning!

You chose to change your boot loader. When converting 
the configuration, some settings may be lost.

The current configuration will be saved and you will be able
to restore it if you return to the current boot loader.

Select a course of action:
")
		// warning - popup
	    : sformat (_("Warning!

Your partitioning setup has changed. It is recommended
to use %1 with the current partitioning setup. 

When converting the configuration, some settings may be lost.
The current configuration will be saved and you will be able
to restore it if you return to the current boot loader.

Switch boot loader?"), BootCommon::getLoaderName (to));

	term contents = `VBox (
		// warning label
	    `Label (label),
	    `VSpacing (1),
	    `RadioButtonGroup (`id (`action), `VBox (
		canConvert (from, to)
		    ? `Left (`RadioButton (`id (`convert),
			// radiobutton
			_("Co&nvert Current Configuration")))
		    : `VSpacing (0),
		type == `proposal ? `VSpacing (0)
		: `Left (`RadioButton (`id (`scratch),
		    // radiobutton
		    _("&Start New Configuration from Scratch"))),
		`Left (`RadioButton (`id (`propose),
		    // radiobutton
		    _("&Propose New Configuration"))),
		Mode::normal
		    ? `Left (`RadioButton (`id (`read),
			// radiobutton
			_("&Read Configuration Saved on Disk")))
		    : `VSpacing (0),
		BootCommon::other_bl[to]:nil == nil || type == `proposal
		    ? `VSpacing (0)
		    : `Left (`RadioButton (`id (`prev),
			// radiobutton
			_("Res&tore Configuration Saved before Conversion")))
	    )),
	`VSpacing (1),
	`HBox (
	    `HStretch (),
	    `PushButton (`id (`ok), `opt (`key_F10), OKButtonLabel ()),
	    `HSpacing (1),
	    `PushButton (`id (`cancel), `opt (`key_F9), CancelButtonLabel ()),
	    `HStretch ()
	));
	UI::OpenDialog (contents);
	UI::ChangeWidget (`id (`convert), `Value, true);
	symbol ret = UI::UserInput ();
	symbol action = UI::QueryWidget (`id (`action), `CurrentButton);
	UI::CloseDialog ();
	if (ret != `ok)
	    return nil;
	return action;
    }

    /**
      * Handle function of widget
      * @param op any operation to handle
      * @param tosave list of operations, which lead to closing & saving widget
      * @return any changed op (if needed)
      */
    global define any loaderTypeSave (any op, list tosave) ``{
	if (! contains (tosave, op))
	    return;

	string old_bl = Bootloader::getLoaderType ();
	string new_bl = UI::QueryWidget (`id (`loader_type), `Value);
	if (old_bl == new_bl)
	    return;

	symbol action = askSwitch (old_bl, new_bl, `user);
	if (nil != action)
	{
	    y2milestone ("Switching bootloader");
	    BootCommon::other_bl[old_bl] = Bootloader::Export ();
	    BootCommon::setLoaderType (new_bl);

            if (action == `scratch)
		Bootloader::Reset ();
            else if (action == `read)
                Bootloader::Read ();
            else if (action == `propose)
	    {
		Bootloader::Reset ();
                Bootloader::Propose ();
	    }
            else if (action == `convert)
                convertSettings (old_bl, new_bl);
            else if (action == `prev)
                Bootloader::Import (BootCommon::other_bl[new_bl]:$[]);

	    BootCommon::fetchSettings ();
            BootCommon::current_section = eval (BootCommon::current_globals);
            BootCommon::current_section_name = `global;
	}
	BootCommon::location_changed = true;
	return nil;
    }


    /**
      * Generic function to create dialog and handle it's events
      * @param widgets list of widget maps
      * @param caption string dialog caption
      * @param contents term contents of dialog
      * @param help string help for the dialog
      * @param next_button string label of next button
      * @param exits list of symbols representing events to exit dialog
      * @param back whether should be Abort/Back buttons shown (true),
      *   or only Cancel button
      * @return symbol wizard sequencer symbol
      */
    global define symbol runDialog (list widgets, string caption,
	term contents, string help, string next_button, list exits,
	boolean back)
    ``{
	Wizard::SetContentsButtons (caption, contents, help,
	    BackButtonLabel (), next_button);
	if (back)
	{
	    Wizard::RestoreBackButton ();
	    Wizard::RestoreAbortButton ();
	}
	else
	{
	    Wizard::ReplaceBackButton(`VSpacing (0));
	    Wizard::ReplaceAbortButton (
		`PushButton (`id (`abort), CancelButtonLabel ()));
	}

	readWidgets (widgets);

	any ret = nil;
	while (! contains (exits, ret))
	{
	    ret = UI::UserInput ();
	    if (ret == `cancel)
		ret = `abort;
	    if (ret == `abort && ! BootCommon::confirmAbort ())
		ret = nil;
	    if (! validateWidgetsIfNeeded (widgets, ret))
		ret = nil;
	    ret = handleChange (widgets, ret);
	}
	return ret;
    }

    /**
      * Redraw the summary text
      */
    global define void redrawSummary () ``{
	UI::ChangeWidget (`id (`summary), `Value,
	    Bootloader::blSummary (
		BootCommon::verbose ? [`verbose, `links] : [`links]));
    }

    /**
      * Run dialog for sections managment
      * @return symbol wizard sequencer symbol
      */
    global define symbol runSectionsDialog () ``{
        list widgets = getWidgets (["sections"]);
        term contents = `HBox (`HSpacing (2), `VBox (
		`VSpacing (1),
                widgets[0, "widget"]:`VSpacing (0),
                `VSpacing (1)
                ), `HSpacing (2));
	// dialog caption
        string caption = _("Boot Loader Setup -- Sections Managment");
        string help = mergeHelps (widgets);
	list exits = getExitEvents (widgets);
	return runDialog (widgets, caption, contents, help, OKButtonLabel (),
	    exits, true);
    }

    /**
      * Run dialog for single section managment
      * @return symbol wizard sequencer symbol
      */
    global define symbol runSingleSectionDialog () ``{
	list widgets = getWidgets (["singlesection"]);
        term contents = `HBox (`HSpacing (2), `VBox (
                `VSpacing (1),
                widgets[0, "widget"]:`VSpacing (0),
		`VSpacing (1)
                ), `HSpacing (2));
	// dialog caption
        string caption = _("Boot Loader Setup -- Sections Managment");
        string help = mergeHelps (widgets);
        list exits = getExitEvents (widgets);
        return runDialog (widgets, caption, contents, help, OKButtonLabel (),
            exits, true);
    }

    /**
      * Run main summary dialog
      * @return symbol wizard sequencer symbol
      */
    global define symbol runSummaryDialog () ``{
	BootCommon::fetchSettings ();
	BootCommon::current_section
	    = eval (BootCommon::current_globals);
	BootCommon::current_section_name = `global;
        list widgets = getWidgets (["globalsection", "manual_edit_button",
	    "resetbutton"]);
        term contents = `HBox (`HSpacing (2), `VBox (
                `VSpacing (1),
                widgets[0, "widget"]:`VSpacing (0),
                `VSpacing (1),
                widgets[1, "widget"]:`VSpacing (0),
                `VSpacing (1)
                ), `HSpacing (2));
	// dialog caption
        string caption = _("Boot Loader Setup");
        string help = mergeHelps (widgets);
        list exits = getExitEvents (widgets);
	string next_button = (Mode::initial || Mode::config)
	    ? AcceptButtonLabel ()
	    : FinishButtonLabel ();

        return runDialog (widgets, caption, contents, help, next_button,
            exits, false);
    }

    /**
      * Run dialog
      * @return symbol for wizard sequencer
      */
    global define symbol runEditFilesDialog () ``{
        map files = Bootloader::blGetFiles ();
	string default = files["default"]:"";
	files = filter (`k, `v, files, ``(k != "default"));
        list filenames = [];
        foreach (`k, `v, files, ``{
            filenames = add (filenames, k);
        });
	term cb = nil;
	if (size (files) > 1)
	    cb = `ComboBox (`id (`filename), `opt (`notify, `hstretch),
		// combobox label
		_("&File Name"), filenames);
	else
	    // label. %1 is name of file (eg. /etc/lilo.conf
	    cb = `Left (`Label (sformat (_("File Name: %1"), filenames[0]:"")));

	term contents = `HBox (`HSpacing (2), `VBox (
            `VSpacing (2),
	    cb,
            `VSpacing (2),
            `MultiLineEdit (`id (`file), `opt (`hstretch, `vstretch),
		// multiline edit header
                _("Fi&le Contents")),
            `VSpacing (2)
	), `HSpacing (2));

	// dialog caption
        string caption = _("Expert Manual Configuration");
        string help = BootCommon::getExpertManualHelp ();

        list exits = getExitEvents ([]);

        Wizard::SetContentsButtons (caption, contents, help,
            BackButtonLabel (), OKButtonLabel ());

	Wizard::RestoreBackButton ();
	Wizard::RestoreAbortButton ();

	string filename = filenames[0]:"";
	if (default != "")
	    filename = default;
	if (size (files) > 1)
	    UI::ChangeWidget (`id (`filename), `Value, filename);
	UI::ChangeWidget (`id (`file), `Value, files[filename]:"");

        any ret = nil;
        while (! contains (exits, ret))
        {
            ret = UI::UserInput ();
	    if (ret == `filename)
	    {
		files[filename] = UI::QueryWidget (`id (`file), `Value);
		filename = UI::QueryWidget (`id (`filename), `Value);
		UI::ChangeWidget (`id (`file), `Value, files[filename]:"");
	    }
	    if (ret == `next)
	    {
		files[filename] = UI::QueryWidget (`id (`file), `Value);
		Bootloader::blSetFiles (files);
		BootCommon::changed = true;
		BootCommon::location_changed = true;
	    }
	    if (ret == `abort)
	    {
		if (! confirmAbortPopup ())
		    ret = nil;
	    }
        }
        return ret;
    }


    /**
      * Get generic aliases
      * @return map aliases
      */
    global define map getAliases () ``{
	return $[
	    "summary" : ``(runSummaryDialog ()),
// general routines
	    "fetch" : [``(BootCommon::fetchSettings ()), true],
	    "store" : [``(BootCommon::storeSettings ()), true],
// expert edit dialog
	    "editfiles" : ``(runEditFilesDialog ()),
// global settings dialogs
	    "globals" : ``(runGlobalsDialog ()),
	    "target" : ``(runTargetDialog ()),
// sections managment dialogs
	    "sects" : ``(runSectionsDialog ()),
	    "singlesect" : ``(runSingleSectionDialog ()),
// sequences definitions
	    "global_seq" :
		``(WizardSequencer (aliases, ws_data["glob_seq"]:$[])),
	    "sect_seq" :
		``(WizardSequencer (aliases, ws_data["sect_seq"]:$[])),
	];
    }


    /**
      * Run wizard sequencer
      * @return `next, `back or `abort
      */
    global define symbol runWizard () ``{

	map glob_seq = $[
	    "ws_start" : "fetch",
	    "fetch" : $[ `next : "globals", `abort: `abort],
	    "globals" : $[ `next: "target", `abort: `abort ],
	    "target" : $[ `next: "store", `abort: `abort ],
	    "store" : $[ `next: `next, `abort: `abort ],
	];

	map sect_seq = $[
	    "ws_start" : "fetch",
	    "fetch" : $[ `next : "sects", `abort: `abort],
	    "sects" : $[ `next: "store", `add : "singlesect",
		`edit : "singlesect", `abort: `abort ],
	    "singlesect" : $[ `next: "sects", `abort: `abort],
	    "store" : $[ `next: `next, `abort: `abort ],
	];

	map normal_sequence = $[
	    "ws_start" : "summary",
	    "summary" : $[
		`next: `next,
		`abort: `abort,
		`glob : "global_seq",
		`manual : "editfiles",
		`sections : "sect_seq",
	     ],
	    "editfiles" : $[ `next : "summary", `abort: `abort ],
	    "global_seq" : $[ `next : "summary", `abort: `abort ],
	    "sect_seq" : $[ `next : "summary", `abort: `abort ],
	];

	map ws_data = $[
	    "start_seq" : "startseq",
	    "startseq" : normal_sequence,
	    "glob_seq" : glob_seq,
	    "sect_seq" : sect_seq,
	];

	// add specific aliases and modify sequence if needed
	ws_data = Bootloader::blGetSequence (ws_data);

	map aliases = getAliases ();
	// merge new aliases
	foreach (`k, `v, ws_data["aliases"]:$[], ``{
	    aliases[k] = v;
	});
	string start_seq = ws_data["start_seq"]:"";
	map sequence = ws_data[start_seq]:$[];

	// run wizard
	y2milestone ("Starting wizard sequencer");
	return WizardSequencer (aliases, sequence);
    }

}
