/**
 * File:
 *      include/bootloader/routines/popups.ycp
 *
 * Module:
 *      Bootloader installation and configuration
 *
 * Summary:
 *      Popups for being used inside bootloader configurator
 *
 * Authors:
 *      Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 *
 */


{

    textdomain "bootloader";

    include "ui/common_messages.ycp";

    import "Report";
    import "Misc";


    /**
      * Display question
      * @return boolean true if answered yes
      */
    global define boolean confirmAbortPopup () ``{
	// yes-no popup question
	return UI::YesNoPopup (_("Really leave the boot loader configuration without saving?
All changes will be lost.
"));
    }

    /**
      * Display question
      * @return boolean true if answered yes
      */
    global define boolean resetSectsPopup () ``{
	// yes-no popup question
	return UI::YesNoPopup (_("Really reset your settings for sections?
All your changes made in section management will be lost.
"));
    }

    /**
      * Display question
      * @param title string section title
      * @return boolean true if answered yes
      */
    global define boolean confirmSectionDeletePopup (string title) ``{
	// yes-no popup question
	return UI::YesNoPopup (sformat(_("Really delete section %1?"), title));
    }

    /**
      * Display question
      * @return boolean true if answered yes
      */
    global define boolean resetAllPopup () ``{
	// yes-no popup question
	return UI::YesNoPopup (_("Really reset all
your settings? All your changes will be lost.
"));
    }

    /**
      * Display error
      */
    global define void setLocationErrorPopup () ``{
	// error popup
	Report::Error (_("Set the boot loader location."));
    }

    /**
      * Display error
      */
    global define void emptyPasswdErrorPopup () ``{
	// error popup
	Report::Error (_("The password must not be empty."));
    }

    /**
      * Display error
      */
    global define void passwdMissmatchPopup () ``{
	// error popup
	Report::Error (_("'Password' and 'Retype password'
do not match. Retype the password."));
    }

    /**
      * Display popup about change of section
      * @param sect_name string section name
      */
    global define void displayDiskChangePopup (string sect_name) ``{
	// message popup, %1 is sectino label
	UI::MessagePopup (sformat (_("The disk settings have changed.
Check section %1 settings.
"), sect_name));
    }

    /**
      * Display popup
      */
    global define void displayFilesEditedPopup () ``{
	// message popup
	UI::MessagePopup (_("The disk settings have changed and you edited boot loader
configuration files manually. Check the boot loader settings.
"));
    }

    /**
      * Ask for change of bootloader location because of device unavailability
      * @param device string currently confiogured device
      * @return boolean yes if shall be reset
      */
    global define boolean askLocationResetPopup (string device) ``{
	// yes-no popup
	return UI::YesNoPopup(sformat(_("Partition '%1' is not available.
Set default boot loader location?
"), device));
    }

    /**
      * Show the popup before saving to floppy, handle actions
      * @return true on success
      */
    global define boolean saveToFLoppyPopup () ``{
	boolean retval = true;
	boolean format = false;
	symbol fs = `no;
	list items = [
		// combobox item
	    `item (`id (`no), _("Do Not Create a File System")),
		// combobox item
	    `item (`id (`ext2), _("Create an ext2 File System")),
	];
	if (SCR::Read (.target.size, "/sbin/mkfs.msdos") != -1)
		// combobox item
	   items = add (items, `item (`id (`fat), _("Create a FAT File System")));
	term contents = `VBox (
		// label
	    `Label (_("The boot loader boot sector will be written
to a floppy disk. Insert a floppy disk
and confirm with OK.
")),
	    `VSpacing (1),
	    // checkbox
	    `Left (`CheckBox (`id (`format), _("&Low Level Format"), false)),
	    `VSpacing (1),
	    // combobox
	    `Left (`ComboBox (`id (`fs), _("&Create File System"), items)),
	    `VSpacing (1),
	    `PushButton (`id (`ok), OKButtonLabel ())
	);
	UI::OpenDialog (contents);
	any ret = nil;
	while (ret != `ok)
	{
	    ret = UI::UserInput ();
	}
	if (ret == `ok)
	{
	    format = UI::QueryWidget (`id (`format), `Value);
	    fs = UI::QueryWidget (`id (`fs), `Value);
	}
	UI::CloseDialog ();
	string dev = BootCommon::loader_device;
	if (format)
	{
	    boolean tmpretval = true;
	    y2milestone ("Low level formating floppy");
	    while (true)
	    {
		tmpretval = 0 == SCR::Execute
		    (.target.bash, sformat ("/usr/bin/fdformat %1", dev));
		if (tmpretval)
		    break;
		// yes-no popup
		if (! UI::YesNoPopup (_("Low level format failed. Try again?")))
		    break;
	    }
	    retval = retval && tmpretval;
	}
	if (fs == `ext2)
	{
	    y2milestone ("Creating ext2 on floppy");
            boolean tmpretval = 0 == SCR::Execute
		(.target.bash, sformat ("/sbin/mkfs.ext2 %1", dev));
	    if (! tmpretval)
		// error report
		Report::Error (_("Creating file system failed."));
            retval = retval && tmpretval;
        }
	else if (fs == `fat)
	{
	    y2milestone ("Creating msdosfs on floppy");
	    boolean tmpretval = 0 == SCR::Execute
		(.target.bash, sformat ("/sbin/mkfs.msdos %1", dev));
	    if (! tmpretval)
		// error report
		Report::Error (_("Creating file system failed."));
	    retval = retval && tmpretval;
	}
	return retval;
    }

    /**
      * Display error
      */
    global define void usedNameErrorPopup () ``{
        // error popup
        Report::Error (_("The name selected is already used.
Use a different one.
"));
    }

    /**
      * Display error
      * @return true if shall retry
      */
    global define boolean writeErrorPopup () ``{
        // yes-no popup
	return UI::YesNoPopup (_("An error occurred during boot loader
installation. Retry boot loader configuration?
"));
    }

    /**
      * Display popup
      */
    global define void displayGfxMenuChangePopup () ``{
	// message popup, gfxmenu is option name, leave as is
	UI::MessagePopup (_("The disk settings have changed.
Check the gfxmenu settings.
"));
    }

    /**
      * Display yes-no popup
      * @return true if confirmed
      */
    global define boolean confirmOptionDeletePopup () ``{
	// yes-no popup
	return UI::YesNoPopup (_("Really delete the selected option?"));
    }

    /**
      * Display error popup with log
      * @param header string error header
      * @param log string logfile contents
      */
    global define void errorWithLogPopup (string header, string log) ``{
	term text = `RichText( "<p>" + log + "</p>" );
	UI::OpenDialog(`opt ( `decorated ),
	    `VBox (`HSpacing(75),
		// heading
		`Heading(header),
		text,     // e.g. `Richtext()
		`PushButton( `id(`ok_help), `opt(`default), OKButtonLabel() )
	    )
	);

	UI::SetFocus(`id(`ok_help) );
	any r = UI::UserInput();
	UI::CloseDialog();
    }

    /**
      * Display popup
      */
    global define void displayNoSupportPopup () ``{
	// message popup
	UI::MessagePopup (_("Sorry, there are currently
no options to set here."));
    }

    /**
      * Display popup
      */
    global define void noBootloaderPopup () ``{
	// error report
	Report::Error (_("Unable to install the boot loader."));
    }

    /**
      * Display popup
      * @param bootloader string printable name of used bootloader
      */
    global define void floppyWrittenPopup (string bootloader) ``{
	string confirm_boot_msg = Misc::boot_msg;
	// data saved to floppy disk
	string msg = sformat (_(
	    // popup, %1 is bootloader name
	    "The %1 boot sector has been written to the floppy disk."),
	    bootloader);
	msg = msg + "\n";
	if (Mode::hardBoot)
        {
            // If LILO was written on floppy disk and we need
            // to do a hard reboot (because a different kernel
            // was installed), tell the user to leave the floppy
            // inserted.
	    msg = msg
		// popup - continuing - alternative 1 ...
                + _("Leave the floppy disk in the drive. The system will now be rebooted.");
	}
	else
	{
	    msg = msg
		// popup - continuing - alternative 2 ...
		+ _("Remove the floppy disk from the drive.");
	}

	if ( size (confirm_boot_msg) > 0 )
	{
	    msg = msg + "\n\n" + confirm_boot_msg;
	}
	Misc::boot_msg = "";
        // empty that  Misc::boot_msg indicate the message has been displayed
	Report::Message(msg);
    }
}
