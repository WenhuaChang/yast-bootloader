/**
 * File:
 *	BootPPC.ycp
 *
 * Module:
 *	BootPPC
 *
 * Summary:
 *	functions for bootload setup
 *
 * Authors:
 *	Klaus Kaempf <kkaempf@suse.de>
 *	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 *
 */
{
    module "BootPPC";

    import "Arch";
    import "Boot";
    import "Kernel";

    global define boolean Write ()
    ``{
	if (!( Arch::board_prep || Arch::board_chrp || Arch::board_mac_new  ))
	    return true;

	string boot_device	= Boot::device;
	string root_device	= Boot::RootPartitionDevice;
	string other_device	= Boot::others[0]:"";
	boolean lilo_activate	= Boot::activate;
	string lilo_conf = "# Generated by YaST2\n
default=linux
timeout=100
boot=" + boot_device + "\n";

	if (lilo_activate == true)
	{
	    lilo_conf = lilo_conf + "activate\n";
	}

	lilo_conf = lilo_conf + "
image = /boot/" + Kernel::binary + "
	label = linux
	root = " + root_device + "\n";

	lilo_conf = lilo_conf + "\tappend = \"" + Boot::parameters +"\"\n\n";

	if (other_device != "")
	{
	    lilo_conf = lilo_conf + "\nother = " + other_device + "\n\tlabel = macos \n\n";
	}

	SCR::Write (.target.string, "/etc/lilo.conf", lilo_conf);

	if (Arch::board_prep)
	{
	    // the PReP boot file (the kernel) is not in k_deflt.rpm
	    // FIXME: this is one of the crudest hacks (cf #9718)
	    SCR::Execute (.target.bash, "/bin/cp -p " + Installation::sourcedir +
			"/suse/images/zImage.prep /boot/zImage.prep");
	    SCR::Execute (.target.bash, "/bin/cp -p " + Installation::sourcedir +
			"/suse/images/zImage.initrd.prep /boot/zImage.initrd.prep");
	}
	SCR::Execute(.target.bash, "/sbin/lilo --lilo-rootdrive " + root_device);
	SCR::Execute(.target.bash, "/usr/lib/YaST2/bin/ppc_postinstall.sh " + boot_device + " " + root_device);

	return true;
    }

}
