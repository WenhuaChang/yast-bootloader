/**
 * File:
 *	BootELILO.ycp
 *
 * Module:
 *	BootELILO
 *
 * Summary:
 *	Functions for elilo setup
 *
 * Authors:
 *	Andreas Schwab <schwab@suse.de>
 *
 * $Id$
 *
 */
{
    module "BootELILO";

    textdomain "bootloader";

    import "Arch";
    import "Boot";
    import "Kernel";

    /**
     * WriteUpdate
     * Calling mk_initrd while update.
     * @ return void
     */
    global define void WriteUpdate()
    ``{
	// Calling mk_initrd
	
	if (SCR::Execute (.target.bash, "/sbin/mk_initrd >> /var/log/YaST2/y2logmk_initrd 2>> /var/log/YaST2/y2logmk_initrd") != 0)
	  {
	    any retAsk = UI::AskShowLog("mk_initrd");
	    if (retAsk == `yes)
	      DisplayLogFile(SCR::Read (.target.string, "/var/log/YaST2/y2logmk_initrd"));
	  }
    }

    /**
     * DisplayLogFile
     * Displaying log files
     * @ return 'ok_help
     */
    global define DisplayLogFile( string msg_text ) ``{
            term text = `RichText( "<p>" + msg_text + "</p>" );
            UI::OpenDialog(`opt ( `decorated ),
                       `VBox (`HSpacing(75),
                                // heading
                              `Heading( _("Error Log File")),
                              text,     // e.g. `Richtext()
                              `PushButton( `id(`ok_help), `opt(`default), OKButtonLabel() )
                              )
                       );

            UI::SetFocus(`id(`ok_help) );
            any r = UI::UserInput();
            UI::CloseDialog();
            return (r);
    };


    /**
     * write the ELILO bootloader
     */

    global define boolean Write ()
    ``{
	import "Mode";
	import "Boot";
	import "Report";
	import "Product";

	if (Mode::update)
	  {
	    WriteUpdate();
	    return true;
	  }

	string docmd = "/usr/lib/YaST2/bin/doelilo /";
	map doenv = $[ "bootdev" : Boot::BootPartitionDevice,
		       "rootdev" : Boot::RootPartitionDevice,
		       "append" : Kernel::cmdLine,
		       "product" : Product::name ];
	y2milestone ("doelilo doenv: %1", doenv);
	integer doresult = SCR::Execute(.target.bash, docmd, doenv);

	if (doresult != 0)
	  {
	    Report::Error (_("Could not write ELILO configuration"));
	  }

	return true;
    }
}
